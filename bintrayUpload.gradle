// bintray插件
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

// 发布到组织名称名字，必须填写
group = BINTRA_PKG_GROUP
// 版本号，下次更新是只需要更改版本号即可
version = BINTRAY_PKG_VERSION
// 发布到JCenter上的项目名字
def libName = BINTRAY_PKG_LIBNAME
// 项目主页
def siteUrl = BINTRAY_PKG_SITEURL
// 项目的git地址
def gitUrl = BINTRAY_PKG_GITURL
// 项目描述
def desc = BINTRAY_PKG_DESC

// 生成Javadoc文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

// 文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// 生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            // 开源协议
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }
    developers {
        developer {
            // 开发者的个人信息
            id BINTRAY_DEVELOPER_ID
            name BINTRAY_DEVELOPER_NAME
            email BINTRAY_DEVELOPER_EMAIL
        }
    }
    scm {
        connection gitUrl
        developerConnection gitUrl
        url siteUrl
    }
}

publishing {
    publications {
        Production(MavenPublication) {
            // 上传到JCenter所需要的源码文件
            artifact sourcesJar
            artifact javadocJar
            groupId group
            artifactId BINTRAY_PKG_ARTIFACT_ID
            version version

            pom.packaging = BINTRAY_PACKAGING
            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def root = asNode()
                root.children().last() + pomConfig

                def dependenciesNode = asNode().appendNode('dependencies')
                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
        Sandbox(MavenPublication) {
            // 上传到JCenter所需要的源码文件
            artifact sourcesJar
            artifact javadocJar
            groupId group
            artifactId BINTRAY_PKG_ARTIFACT_ID
            version version

            pom.packaging = BINTRAY_PACKAGING
            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def root = asNode()
                root.children().last() + pomConfig

                def dependenciesNode = asNode().appendNode('dependencies')
                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

// 上传到JCenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    // 读取 项目根目录下local.properties 文件里面的 BINTRAY_USER
    user = properties.getProperty("BINTRAY_USER")
    // 读取 项目根目录下local.properties 文件里面的 BINTRAY_APIKEY
    key = properties.getProperty("BINTRAY_APIKEY")
    configurations = ['archives']
    pkg {
        // 这里的repo值必须要和你创建Maven仓库的时候的名字一样
        repo = "maven"
        // 发布到JCenter上的项目名字
        name = libName
        // 项目描述
        desc = desc
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
    publications = ['Production', 'Sandbox']
}